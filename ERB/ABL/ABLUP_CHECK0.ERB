;ARG->実行する番号(-1でチェックのみ, -2で表示あり), ARG:1->チェックする奴隷
@CHECK_ABLUP_AVAILABLE_0, ARG, ARG:1

IF ARG:1 == 0
	ARG:1 = TARGET
ENDIF

VARSET LOCAL, 0

;TARGETを避難
LOCAL:0 = TARGET
TARGET = ARG:1

;既にLv5
IF ABL:0 >= 5
	SIF ARG == -2
		PRINTW 既にMAXです
	RETURN 0
ENDIF

;LOCAL:1
;LOCAL:2
;LOCAL:3
;LOCAL:4

;結果
;LOCAL:998

;臆病をみて、あるなら1
LOCAL:100 = TALENT:10

SELECTCASE ABL:0
	CASE 0
		LOCAL:50 = 10
		SIF LOCAL:100
			LOCAL:50 = 1
		LOCAL:51 = 10
		LOCAL:52 = 300
		LOCAL:53 = 200
	CASE 1
		LOCAL:50 = 150
		SIF LOCAL:100
			LOCAL:50 = 10
		LOCAL:51 = 100
		LOCAL:52 = 1000
		LOCAL:53 = 1200
	CASE 2
		LOCAL:50 = 1000
		SIF LOCAL:100
			LOCAL:50 = 50
		LOCAL:51 = 800
		LOCAL:52 = 2000
		LOCAL:53 = 3000
	CASE 3
		LOCAL:50 = 3000
		SIF LOCAL:100
			LOCAL:50 = 600
		LOCAL:51 = 3000
		LOCAL:52 = 0
		LOCAL:53 = 0
	CASE 4
		LOCAL:50 = 8000
		SIF LOCAL:100
			LOCAL:50 = 3000
		LOCAL:51 = 15000
		LOCAL:52 = 0
		LOCAL:53 = 0
ENDSELECT

IF TALENT:27
	SELECTCASE ABL:0
		CASE 3
			TIMES LOCAL:50 , 2.00
			TIMES LOCAL:51 , 2.00
		CASE 4
			TIMES LOCAL:50 , 3.00
			TIMES LOCAL:51 , 3.00
	ENDSELECT
ENDIF

;気丈
IF TALENT:12
	TIMES LOCAL:50 , 3.00
	TIMES LOCAL:51 , 2.00
	TIMES LOCAL:52 , 1.50
	TIMES LOCAL:53 , 1.50
ENDIF

;生意気
IF TALENT:16
	TIMES LOCAL:50 , 1.50
	TIMES LOCAL:51 , 2.00
	TIMES LOCAL:53 , 1.50
ENDIF

;素直
IF TALENT:13
	TIMES LOCAL:50 , 0.50
	TIMES LOCAL:51 , 0.50
	TIMES LOCAL:53 , 0.50
ENDIF

;恋慕
IF TALENT:13
	TIMES LOCAL:51 , 0.50
ENDIF

;Lv4 -> Lv5
IF ABL:0 == 4
	;[臆病][素直][妄信]なら無視
	IF TALENT:10 == 0 && TALENT:13 == 0 && TALENT:86 == 0
		SIF ARG == -2
			PRINTFORML %EXPNAME:50%あり、かつ
		IF EXP:50 == 0
			LOCAL:1 |= 2
			LOCAL:2 |= 2
			LOCAL:3 |= 2
		ENDIF
	ENDIF
ENDIF

;恐怖の珠で上げる
SIF LOCAL:50 > 0 && JUEL:10 < LOCAL:50
	LOCAL:1 |= 1
	
;恭順の珠で上げる
SIF LOCAL:51 > 0 && JUEL:4 < LOCAL:51
	LOCAL:2 |= 1

;欲情の珠で上げる
SIF LOCAL:52 > 0 && JUEL:5 < LOCAL:52
	LOCAL:3 |= 1
	
;屈服の珠で上げる
SIF LOCAL:53 > 0 && JUEL:6 < LOCAL:53
	LOCAL:4 |= 1

;チェックのみなら返却
IF ARG == -1
	TARGET = LOCAL:0

	SIF LOCAL:1 == 0 && LOCAL:50 != 0
		RETURN 1
	
	SIF LOCAL:2 == 0 && LOCAL:51 != 0
		RETURN 1
	
	SIF LOCAL:3 == 0 && LOCAL:52 != 0
		RETURN 1
	
	SIF LOCAL:4 == 0 && LOCAL:53 != 0
		RETURN 1
		
	RETURN 0
ENDIF

IF ARG == -2
	;表記1
	IF LOCAL:50 > 0
		CALL PRINT_UPGRADE_AVAILABLE_PREFIX, LOCAL:1, 0
		CALL PRINT_UPGRADE_AVAILABLE_JUEL, 10, LOCAL:50
		CALL PRINT_UPGRADE_AVAILABLE_STATUS, LOCAL:1
		CALL PRINT_UPGRADE_AVAILABLE_POSTFIX, LOCAL:1
	ENDIF

	;表記2
	IF LOCAL:51 > 0
		CALL PRINT_UPGRADE_AVAILABLE_PREFIX, LOCAL:2, 1
		CALL PRINT_UPGRADE_AVAILABLE_JUEL, 4, LOCAL:51
		CALL PRINT_UPGRADE_AVAILABLE_STATUS, LOCAL:2
		CALL PRINT_UPGRADE_AVAILABLE_POSTFIX, LOCAL:2
	ENDIF
	
	;表記3
	IF LOCAL:52 > 0
		CALL PRINT_UPGRADE_AVAILABLE_PREFIX, LOCAL:3, 2
		CALL PRINT_UPGRADE_AVAILABLE_JUEL, 5, LOCAL:52
		CALL PRINT_UPGRADE_AVAILABLE_STATUS, LOCAL:3
		CALL PRINT_UPGRADE_AVAILABLE_POSTFIX, LOCAL:3
	ENDIF
	
	;表記4
	IF LOCAL:53 > 0
		CALL PRINT_UPGRADE_AVAILABLE_PREFIX, LOCAL:4, 3
		CALL PRINT_UPGRADE_AVAILABLE_JUEL, 6, LOCAL:53
		CALL PRINT_UPGRADE_AVAILABLE_STATUS, LOCAL:4
		CALL PRINT_UPGRADE_AVAILABLE_POSTFIX, LOCAL:4
	ENDIF
	
	PRINTL [10] - やめる
ENDIF

INPUT
SIF RESULT == 10
	RETURN 0

IF RESULT < 0 || RESULT >= 4
	RESTART
ENDIF

IF (LOCAL:1 && RESULT == 0) || (LOCAL:2 && RESULT == 1) || (LOCAL:3 && RESULT == 2) || (LOCAL:4 && RESULT == 3)
	PRINTL 条件を満たしていません
	RESTART
ENDIF

SIF RESULT == 0 && LOCAL:50 == 0
	RESTART
SIF RESULT == 1 && LOCAL:51 == 0
	RESTART
SIF RESULT == 2 && LOCAL:52 == 0
	RESTART
SIF RESULT == 3 && LOCAL:53 == 0
	RESTART

ABL:0 += 1

PRINTFORMW %CALLNAME%の%ABLNAME:0%が{ABL:0}になりました

SELECTCASE RESULT
	CASE 0
		JUEL:10 -= LOCAL:50
	CASE 1
		JUEL:4 -= LOCAL:51
	CASE 2
		JUEL:5 -= LOCAL:52
	CASE 3
		JUEL:6 -= LOCAL:53
ENDSELECT

TARGET = LOCAL:0

CALL DISPLAY_KOJO_MESSAGE_PALAMCNG_SLAVE, 0, ABL:0
CALL DISPLAY_KOJO_MESSAGE_PALAMCNG_ASSI, 0, ABL:0