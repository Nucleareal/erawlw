@EVENTFIRST

;変数初期化
MASTER =  0
TARGET = -1
ASSI   = -1
PLAYER = MASTER
PBAND = 4

MONEY = 1000

;期限
FLAG:999 = 0

DAY:101 = 80
DAY:102 = 70
DAY:103 = 60
DAY:111 = 365
MONEY:101 = 1000000
MONEY:102 = 1500000
MONEY:103 = 3000000
MONEY:111 = 0


;ゲームモード選択
CALL SELECT_GAMEMODE
;難易度選択
SIF FLAG:4 < 10
	CALL SELECT_DIFFICULTY

;プロローグ
;Storyモードだとプロローグを表示するか聞く
SIF FLAG:4 == 0 || FLAG:4 == 1
	CALL PROLOGUE_SELECT_STORY_WLW

LOADGLOBAL
FLAG:10 = GLOBAL:400

;PRINTFORMW （{MONEY}リーフで調教したいキャラのカードを買ってください）
;PRINTW （お金があまればほかのアイテムも買いましょう）

;REPEAT 100
;	SIF EXISTCSV(COUNT) == 0
;		CONTINUE
;
;	SIF COUNT == MASTER
;		CONTINUE
;
;	SIF CSVNAME(COUNT) == ""
;		CONTINUE
;	
;	CALL CALC_TRAIN_DIFFICULTY, COUNT
;	DEBUGPRINTFORML %CSVNAME(COUNT)% -> {RESULT}
;REND

BEGIN SHOP

;コマンド終わりの処理
@EVENTCOMEND
#PRI
IF BASE:0 <= 0
	PRINTW ・・・・・・・・・
	PRINTW ・・・・・・
	PRINTW ・・・
	
	PRINTFORMW %NAME:MASTER%の度を超えた苛烈な調教により、%NAME%は息を引き取った。
	
	PRINTW ・・・・・・・・・
	PRINTW ・・・・・・
	PRINTW ・・・

	DRAWLINE
	PRINTL ------------------------------------GAMEOVER-----------------------------------
	DRAWLINE
	WAIT

	QUIT
ELSEIF BASE:0 < 500
	PRINTW （体力が限界に来ています。調教を終了します）
	BEGIN AFTERTRAIN
ENDIF

;-------------------------------------------------
; MASTERの射精回数チェック
;-------------------------------------------------
IF PLAYER == MASTER
	CALL TRAIN_JUDGE_EJAC_MASTER
	IF RESULT
		PRINTFORMW （%CALLNAME:MASTER%の性器が限界に来ています。調教を終了します）
		BEGIN AFTERTRAIN
	ENDIF
ENDIF

;ターンエンド
@EVENTTURNEND

SIF FLAG:0
	CALL PASSIVE_REST

REPEAT CHARANUM
	;体力の回復（午後の調教後は夜の休みが入るので回復大きい）
	IF TIME == 0 && TALENT:COUNT:88 == 0
		A = MAXBASE:COUNT:0 / 20
	ELSE
		A = MAXBASE:COUNT:0 / 4
	ENDIF
	
	;休憩フラグ（「何もしない」を選択）
	SIF FLAG:0
		A += 100

	;回復早い、回復遅い
	IF TALENT:COUNT:111
		A *= 2
	ELSEIF TALENT:COUNT:112
		A /= 2
	ENDIF

	BASE:COUNT:0 += A
	SIF BASE:COUNT:0 > MAXBASE:COUNT:0
		BASE:COUNT:0 = MAXBASE:COUNT:0

	;気力の回復
	BASE:COUNT:1 = MAXBASE:COUNT:1
REND

FLAG:0 = 0

;午後なら次の日、午前なら午後にする
IF TIME == 1
	DAY = DAY+1
	
	CALL DAY_INCREMENT_EVENT
	
	PRINTW 一日が終わった・・・

	TIME = 0
ELSE
	TIME = 1
ENDIF

BEGIN SHOP




@SELECT_GAMEMODE
FLAG:4 = 0

DRAWLINE
PRINTL モードを選択してください
PRINTL 
DRAWLINE

PRINTL [ 0]STORY
PRINTL [ 1]ABNORMAL
PRINTL [10]PRACTICE
  
PRINTL [99]HELP   （各ゲームモードの簡単な説明）

$INPUT_LOOP_GAMEMODE
INPUT
IF RESULT == 0
	PRINTW STORYでプレイします
	FLAG:4 = 0
	MONEY = 1000
	TALENT:MASTER:998 = 0
ELSEIF RESULT == 1
	PRINTW ABNORMALでプレイします
	FLAG:4 = 1
	MONEY = 1000
	TALENT:MASTER:998 = 0
	
	CALL SET_MASTER_SEX
ELSEIF RESULT == 10
	PRINTW PRACTICEモードを選択しました
	MONEY = 10000000
	TALENT:MASTER:998 = 1
	FLAG:4 = 10
ELSEIF RESULT == 99
	DRAWLINE
	PRINTL STORY     ：【期限あり】普通にプレイをする人向け
	PRINTL ABNORMAL  ：【期限あり】女主人公で始めたい人向け
	PRINTL PRACTICE  ：【期限なし】練習用モード
	DRAWLINE
	WAIT
	DRAWLINE
	DRAWLINE
	DRAWLINE
	RESTART
ELSEIF RESULT == 393
	PRINTW ミクサモードを選択しました
	MONEY = 1000
	FLAG:4 = 101
ELSE
	CLEARLINE 1
	REUSELASTLINE 正しい値を入力して下さい
	GOTO INPUT_LOOP_GAMEMODE
ENDIF



;性別設定
@SET_MASTER_SEX
PRINTL あなたの性別は？
PRINTLC [0]♂
PRINTLC [1]♀
PRINTL 
$INPUT_LOOP_5
INPUT
IF RESULT == 0
	;男なら処女はない
	TALENT:MASTER:0 = 0
	;ふたなり
	TALENT:MASTER:121 = 0
	;オトコ
	TALENT:MASTER:122 = 1

	MAXBASE:MASTER:2 = 10000
	PRINTW ♂で始めます
ELSEIF RESULT == 1
	;処女
	TALENT:MASTER:0 = 1
	;ふたなり
	TALENT:MASTER:121 = 0
	;オトコ
	TALENT:MASTER:122 = 0
	
	TALENT:MASTER:81 = 1
	ABL:MASTER:9 = 5
	ABL:MASTER:12 = 5
	
	MAXBASE:MASTER:2 = 0
	PRINTW ♀で始めます
	
ELSEIF RESULT == 6576736769
	;処女
	TALENT:MASTER:0 = 1
	;ふたなり
	TALENT:MASTER:121 = 1
	;オトコ
	TALENT:MASTER:122 = 0
	
	TALENT:MASTER:81 = 1
	ABL:MASTER:9 = 5
	ABL:MASTER:12 = 5

	MAXBASE:MASTER:2 = 10000
	
	PRINTW ふたなりで始めます
ELSE
	GOTO INPUT_LOOP_5
ENDIF

;そこまで影響はないがイメージ的に初期の体力・気力を変更。単なるイメージってことで
MAXBASE:MASTER:0 = (TALENT:MASTER:122) ? 2500 # 1800
MAXBASE:MASTER:1 = (TALENT:MASTER:122) ? 2000 # 1500

;全快処理
BASE:MASTER:0 = MAXBASE:MASTER:0
BASE:MASTER:1 = MAXBASE:MASTER:1

PRINTL
DRAWLINE

@SELECT_DIFFICULTY
DRAWLINE
PRINTL 難易度を選択してください
PRINTL 
DRAWLINE

PRINTFORML [  1]初心者調筆使い   :{DAY:101,3}日間 | 目標金額{MONEY:101,8}リーフ
PRINTFORML [  2]中堅調筆使い     :{DAY:102,3}日間 | 目標金額{MONEY:102,8}リーフ
PRINTFORML [  3]ベテラン調筆使い :{DAY:103,3}日間 | 目標金額{MONEY:103,8}リーフ
PRINTFORML [???]究極の調筆使い   :{DAY:111,3}日間 | 目標: 全女性キャストの調教完了

PRINTL [100]HELP   （各難易度の簡単な説明）

$INPUT_LOOP_DIFFICULTY
INPUT

FLAG:3 = RESULT

IF RESULT == 100
	DRAWLINE
	PRINTFORML 初心者調筆使い   : %GAMEBASE_TITLE%を初めてプレイする方向け
	PRINTL 中堅調筆使い     : 初心者の難易度に飽きてきた人向け
	PRINTL ベテラン調筆使い : 調教を極めたい人向け
	PRINTL 究極の調筆使い   : 選択するな
	DRAWLINE
	WAIT
	DRAWLINE
	DRAWLINE
	DRAWLINE
	RESTART
ELSEIF RESULT == 1
	PRINTW 難易度「初心者調筆使い」を選択しました
ELSEIF RESULT == 2
	PRINTW 難易度「中堅調筆使い」を選択しました
ELSEIF RESULT == 3
	PRINTW 難易度「ベテラン調筆使い」を選択しました
	MONEY += 2000
ELSEIF RESULT == 365
	PRINTW 難易度「究極の調筆使い」を選択しました
	
	ABL:MASTER:2 = 1
	
	VARSET LOCAL , 0
	
	REPEAT 32
		LOCAL = COUNT + 1 + 100
		
		SIF ITEMNAME:COUNT != ""
			MONEY += ITEMPRICE:LOCAL
	REND
	
	MONEY += 10000
ELSE
	GOTO INPUT_LOOP_DIFFICULTY
ENDIF

SELECTCASE FLAG:3
	CASE 1
		FLAG:998 = MONEY:101
	CASE 2
		FLAG:998 = MONEY:102
	CASE 3
		FLAG:998 = MONEY:103
ENDSELECT

SELECTCASE FLAG:3
	CASE 1
		FLAG:999 = DAY:101
	CASE 2
		FLAG:999 = DAY:102
	CASE 3
		FLAG:999 = DAY:103
	CASE 365
		FLAG:999 = DAY:111
ENDSELECT