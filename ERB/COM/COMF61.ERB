@COM61
;クンニ強制

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:1
	SIF S
		PRINT  + 
	A += ABL:1 * 1
	PRINTS ABLNAME:1
	PRINTV 'LV,ABL:1,'(,ABL:1 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:6
	SIF S
		PRINT  + 
	A += ABL:6 * 4
	PRINTS ABLNAME:6
	PRINTV 'LV,ABL:6,'(,ABL:6 * 4,')
	S = 1
ENDIF

IF MARK:1
	SIF S
		PRINT  + 
	A += MARK:1 * 2
	PRINTS MARKNAME:1
	PRINTV 'LV,MARK:1,'(,MARK:1 * 2,')
	S = 1
ENDIF


;PALAM:欲情
IF PALAM:5 < PALAMLV:1
	L = 0
ELSEIF PALAM:5 < PALAMLV:2
	L = 1
ELSEIF PALAM:5 < PALAMLV:3
	L = 2
ELSEIF PALAM:5 < PALAMLV:4
	L = 3
ELSEIF PALAM:5 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 1
	PRINTS PALAMNAME:5
	PRINTV 'LV,L,'(,L * 1,')
	S = 1
ENDIF

;恥じらい
IF TALENT:35
	PRINT  - 
	A -= 1
	PRINTS TALENTNAME:35
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:61
	SIF S
		PRINT  + 
	A += 3
	PRINTS TALENTNAME:61
	PRINTV '(,3,')
	S = 1
ENDIF
;汚臭敏感
IF TALENT:62
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:62
	PRINTV '(,3,')
	S = 1
ENDIF
;献身的
IF TALENT:63
	SIF S
		PRINT  + 
	A += 6
	PRINTS TALENTNAME:63
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定
IF TALENT:71
	PRINT  - 
	A -= 1
	PRINTS TALENTNAME:71
	PRINTV '(,1,')
	S = 1
ENDIF
;恋慕
IF TALENT:85 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:85
	PRINTV '(,5,')
	S = 5
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:3 & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:3 & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:3 & 8
	Y += 7
SIF STAIN:PLAYER:3 & 16
	Y += 15

SIF TALENT:61
	Y /= 3
SIF TALENT:62
	Y *= 2

;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF TALENT:61
		PRINT 汚れあり、
		PRINTS TALENTNAME:61
	ELSEIF TALENT:62
		PRINT 汚れあり、
		PRINTS TALENTNAME:62
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF


;合計を表示(23以上で実行)
PRINT  = 
PRINTV A

V = 23
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
IF A < V
	CALL DISPLAY_KOJO_DENIAL_MESSAGE_COM, 61
	RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = クンニ強制
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM
;-------------------------------------------------
;射精ゲージチェック（ふたなりの場合）
;-------------------------------------------------
B = 0

;ABL:技巧をみる
IF ABL:2 == 0
	B = 100
ELSEIF ABL:2 == 1
	B = 500
ELSEIF ABL:2 == 2
	B = 900
ELSEIF ABL:2 == 3
	B = 1200
ELSEIF ABL:2 == 4
	B = 1500
ELSE
	B = 2000
ENDIF

;ABL:従順をみる
IF ABL:0 == 0
	TIMES B , 0.80
ELSEIF ABL:0 == 1
	TIMES B , 0.90
ELSEIF ABL:0 == 2
	TIMES B , 1.00
ELSEIF ABL:0 == 3
	TIMES B , 1.10
ELSEIF ABL:0 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;ABL:奉仕精神をみる
IF ABL:6 == 0
	TIMES B , 0.50
ELSEIF ABL:6 == 1
	TIMES B , 0.80
ELSEIF ABL:6 == 2
	TIMES B , 1.20
ELSEIF ABL:6 == 3
	TIMES B , 1.50
ELSEIF ABL:6 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.40
ENDIF

;TALENT:舌使いをみる
SIF TALENT:52
	TIMES B , 2.00

;プレイヤーのABL:Ｃ感覚をみる
IF ABL:PLAYER:3 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:3 == 1
	TIMES B , 1.50
ELSEIF ABL:PLAYER:3 == 2
	TIMES B , 2.00
ELSEIF ABL:PLAYER:3 == 3
	TIMES B , 2.50
ELSEIF ABL:PLAYER:3 == 4
	TIMES B , 3.50
ELSE
	TIMES B , 5.00
ENDIF

SIF TALENT:PLAYER:121 || TALENT:PLAYER:122
	BASE:PLAYER:2 += B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:1 += 10
LOSEBASE:1 += 100

SOURCE:13 = 1000
SOURCE:14 = 500

;上のほうで計算した汚れデータ
SOURCE:8 = Y*80 + 50

;ABL:奉仕精神をみる
IF ABL:6 == 0
	SOURCE:4 = 420
	SOURCE:5 = 150
	TIMES SOURCE:8 , 4.00
ELSEIF ABL:6 == 1
	SOURCE:4 = 500
	SOURCE:5 = 300
	TIMES SOURCE:8 , 2.50
ELSEIF ABL:6 == 2
	SOURCE:4 = 580
	SOURCE:5 = 600
	TIMES SOURCE:8 , 1.50
ELSEIF ABL:6 == 3
	SOURCE:4 = 660
	SOURCE:5 = 900
	TIMES SOURCE:8 , 1.00
ELSEIF ABL:6 == 4
	SOURCE:4 = 740
	SOURCE:5 = 1500
	TIMES SOURCE:8 , 0.50
ELSE
	SOURCE:4 = 820
	SOURCE:5 = 2200
	TIMES SOURCE:8 , 0.10
ENDIF


;ABL:技巧をみる
IF ABL:2 == 0
	TIMES SOURCE:4 , 0.50
	TIMES SOURCE:5 , 0.50
ELSEIF ABL:2 == 1
	TIMES SOURCE:4 , 0.80
	TIMES SOURCE:5 , 0.80
ELSEIF ABL:2 == 2
	TIMES SOURCE:4 , 1.00
	TIMES SOURCE:5 , 1.00
ELSEIF ABL:2 == 3
	TIMES SOURCE:4 , 1.50
	TIMES SOURCE:5 , 1.50
ELSEIF ABL:2 == 4
	TIMES SOURCE:4 , 2.50
	TIMES SOURCE:5 , 2.50
ELSE
	TIMES SOURCE:4 , 4.00
	TIMES SOURCE:5 , 4.00
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
S = BASE:PLAYER:2
EJAC = MAXBASE:PLAYER:2

IF  S > EJAC * 2
	E = 2
ELSEIF S > EJAC
	E = 1
ELSE
	E = 0
ENDIF

IF E
;射精している
	TIMES SOURCE:4 , 3.00

	;ABL:精液中毒をみる
	IF ABL:11 == 0
		SOURCE:7 = 0
		TIMES SOURCE:5 , 2.00
		TIMES SOURCE:13, 2.00
	ELSEIF ABL:11 == 1
		SOURCE:7 = 200
		TIMES SOURCE:5 , 2.50
		TIMES SOURCE:13, 1.60
	ELSEIF ABL:11 == 2
		SOURCE:7 = 500
		TIMES SOURCE:5 , 3.00
		TIMES SOURCE:13, 1.00
	ELSEIF ABL:11 == 3
		SOURCE:7 = 1200
		TIMES SOURCE:5 , 4.50
		TIMES SOURCE:13, 0.70
	ELSEIF ABL:11 == 4
		SOURCE:7 = 2500
		TIMES SOURCE:5 , 6.00
		TIMES SOURCE:13, 0.40
	ELSE
		SOURCE:7 = 5000
		TIMES SOURCE:5 , 8.00
		TIMES SOURCE:13, 0.10
	ENDIF
ENDIF

;大量射精
IF E == 2
	TIMES SOURCE:7 , 2.00
	TIMES SOURCE:5 , 1.50

	EXP:PLAYER:3 += 2
	EXP:20 += 4
	PRINTL 大量射精
	PRINTL 精液経験＋４
	;精液汚れ
	STAIN:PLAYER:2 |= 4

	BASE:PLAYER:2 -= EJAC*2
	SIF BASE:PLAYER:2 >= EJAC
		BASE:PLAYER:2 = EJAC-1

	;クンニで射精させたフラグ
	TFLAG:5 = 2
;通常の射精
ELSEIF E == 1
	EXP:PLAYER:3 += 1
	EXP:20 += 1
	PRINTL 射精
	PRINTL 精液経験＋１
	;精液汚れ
	STAIN:PLAYER:2 |= 4

	BASE:PLAYER:2 -= EJAC
	SIF BASE:PLAYER:2 >= EJAC
		BASE:PLAYER:2 = EJAC-1

	;クンニで射精させたフラグ
	TFLAG:5 = 1
ENDIF

;奴隷の口⇔調教者のＶの汚れが移動
STAIN:0 |= STAIN:PLAYER:3
STAIN:PLAYER:3 |= STAIN:0

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +6
	EXP:40 += 6
	SIF PLAYER != MASTER
		EXP:PLAYER:40 += 6
ENDIF

TFLAG:100 = 1

;屈服刻印１に相当
TFLAG:200 = 1

RETURN 1

