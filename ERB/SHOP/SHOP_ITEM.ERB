;=============================================================================
;アイテム関連の処理
;=============================================================================
;--------------------------------------------------
;アイテム購入処理
;--------------------------------------------------
@TOOL_BUY_MAIN
;売られているアイテムを確認
CALL SALEITEM_CHECK

DRAWLINE
PRINTFORML 調教道具を購入できます。何を買いますか？(残り{MONEY}リーフ)
PRINT_ITEM
DRAWLINE
;売り物表示
REPEAT 100
	SIF ITEMSALES:COUNT == 0
		CONTINUE
		
	SIF ITEMNAME:COUNT == ""
		CONTINUE

	N = COUNT
	IF ITEMSALES:N
		N:1 = ITEMSALES:N
		SIF N:1 <= 1
			N:1 = 0
		PRINTFORMC [{N,2,RIGHT}]%ITEMNAME:N,16,LEFT%({N:1,8,RIGHT}リーフ) 
	ENDIF
REND

LINEISEMPTY
SIF RESULT == 0
	PRINTL

DRAWLINE
PRINTLC [100] 戻る

$INPUT_LOOP
INPUT

N:1 = ITEMPRICE:RESULT
SIF N:1 == 1
	N:1 = 0

IF RESULT == 100
	VARSET ITEMSALES, 0
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 100 || ITEMSALES:RESULT == 0
	CLEARLINE 1
	GOTO INPUT_LOOP
ELSEIF  MONEY < N:1
	CLEARLINE 1
	REUSELASTLINE 所持金が足りません
	GOTO INPUT_LOOP
ENDIF

LOCAL = RESULT
;消耗品
IF LOCAL >= 20 && LOCAL < 30
	CALL BUY_PLURAL, LOCAL
ELSE
	PRINTFORMW ＜%ITEMNAME:LOCAL%を購入しました＞
	MONEY -= N:1
	ITEM:LOCAL = 1
	
	;ローター
	IF ITEM:0
		ITEM:90 = 1
		ITEM:91 = 1
	ENDIF
	
	CALL ITEM_BOUGHT
ENDIF

RESTART

;--------------------------------------------------
;アイテム出現条件
;--------------------------------------------------
@SALEITEM_CHECK
;アイテム・奴隷陳列フラグの削除
VARSET ITEMSALES, 0

;アイテム陳列
FOR LOCAL, 0, 50
	ITEMSALES:LOCAL = ITEMPRICE:LOCAL

	SIF ITEM:LOCAL > 0 && (LOCAL < 20 || LOCAL >= 30)
		ITEMSALES:LOCAL = 0
NEXT

; TODO: ENGAGE
ITEMSALES:50 = 0
;調教対象がいて[恋慕]持ち
IF TARGET > 0 && TALENT:TARGET:85
	SIF ABL:0 % 5 == 0
		GOTO ENGAGE_RING_END
		
	SIF ABL:1 >= 4
		GOTO ENGAGE_RING_END
		
	SIF ABL:3 == 5 || ABL:4 == 5 || ABL:5 == 5
		GOTO ENGAGE_RING_END
		
	SIF ABL:8 >= 2
		GOTO ENGAGE_RING_END
	
	;レズっ気がある場合
	SIF TALENT:122 == 0 && TALENT:MASTER:122 && ABL:9 >= 1
		GOTO ENGAGE_RING_END
		
	SIF MARK:0 >= 1 || MARK:1 == 3 || MARK:2 == 3 || MARK:3 >= 1
		GOTO ENGAGE_RING_END
		
	SIF TALENT:666 || TALENT:89
		GOTO ENGAGE_RING_END
		
	ITEMSALES:50 = ITEMPRICE:50
ENDIF
$ENGAGE_RING_END

;ペアリング
ITEMSALES:51 = 0
IF TARGET > 0 && ASSI > 0
	SIF TALENT:TARGET:89 && CFLAG:TARGET:789 == ASSI && TALENT:ASSI:89 && CFLAG:ASSI:789 == TARGET
		ITEMSALES:51 = ITEMPRICE:51
ENDIF



ITEMSALES:40 = 0

REPEAT CHARANUM
	SIF COUNT == 0
		CONTINUE

	SIF NO:COUNT != 7
		CONTINUE
	
	IF ABL:COUNT:0 >= 4 && ITEM:40 == 0
		ITEMSALES:40 = ITEMPRICE:40
		BREAK
	ENDIF
REND

;--------------------------------------------------
;複数個所持可能なアイテム購入処理
;--------------------------------------------------
;複数個所持可能なアイテムについて、同時購入個数を選択
;LOCALは購入可能個数を示す
@BUY_PLURAL, ARG
LOCAL = MIN(MONEY / ITEMPRICE:ARG, MAX(99-ITEM:ARG, 0))
DRAWLINE
PRINTFORML %ITEMNAME:ARG%をいくつ買いますか？(残り{MONEY}リーフ)
PRINTFORML {LOCAL}個まで購入できます(現在{ITEM:ARG}個所持)
PRINTLC [0] やめる
PRINTLC [1] 1個
SIF LOCAL >= 5
	PRINTLC [5] 5個
SIF LOCAL >= 10
	PRINTLC [10] 10個
SIF LOCAL != 1 && LOCAL != 5 && LOCAL != 10
	PRINTFORMLC [{LOCAL}] 買えるだけ
$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF RESULT < 0
	CLEARLINE 1
	REUSELASTLINE  
	GOTO INPUT_LOOP
ELSEIF ITEMPRICE:ARG * RESULT > MONEY
	CLEARLINE 1
	REUSELASTLINE お金が足りません
	GOTO INPUT_LOOP
ELSEIF RESULT > LOCAL
	CLEARLINE 1
	REUSELASTLINE そんなに持てません
	GOTO INPUT_LOOP
ELSE
	PRINTFORMW ＜%ITEMNAME:ARG%を{RESULT}個購入しました＞
	ITEM:ARG += RESULT
	MONEY -= ITEMPRICE:ARG * RESULT
ENDIF